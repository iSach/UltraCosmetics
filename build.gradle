allprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = 'be.isach'
    version = '2.0'

    compileJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        shaded
        compile.extendsFrom shaded
    }

    processResources {
        inputs.property("version", project.version)

        filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                'VERSION': project.version.split('-')[0]
        ]
    }

    afterEvaluate {
        jar {
            dependsOn configurations.shaded
            from configurations.shaded.collect {
                it.isDirectory() ?
                        it :
                        zipTree(it)
            }
        }
    }
}

boolean local = true

if (local) libsDirName = '../testserver/plugins'

def fullSupport = [':core', ':v1_15_R1']

task fullJar(type: Jar, dependsOn: fullSupport.collect { it + ":compileJava" }) {
    baseName = 'UltraCosmetics'
    from files(fullSupport.collect { project(it).sourceSets.main.output })
}

dependencies {
    subprojects.each { p ->
        shaded(p) {
            transitive false
        }
    }
}

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        // Vault
        maven {
            url 'http://nexus.hc.to/content/repositories/pub_releases'
        }
        // Lib's Disguises
        maven {
            url 'http://repo.md-5.net/content/groups/public/'
        }

        maven {
            url 'http://maven.elmakers.com/repository/'
        }

        maven {
            url 'https://repo.aikar.co/nexus/content/groups/aikar'
        }

        maven {
            url 'https://repo.isach.be'
        }

        // BungeeCord
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }

        // PlaceholderAPI
        maven {
            url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
        }

        // PlayerPoints
        maven {
            url 'https://repo.dakanilabs.com/repository/public/'
        }

        // Citizens
        maven {
            url 'https://repo.citizensnpcs.co/'
        }
    }
}
